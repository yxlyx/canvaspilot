const std = @import("std");

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const merjs_dep = b.dependency("merjs", .{});
    const mer_mod = merjs_dep.module("mer");
    const runtime_mod = merjs_dep.module("runtime");

    // Shared `lib` module — re-exports config, session, backend client, mock
    // data, time helpers and shared API types. Pages do `@import("lib")`.
    const lib_mod = b.createModule(.{
        .root_source_file = b.path("src/lib.zig"),
        .link_libc = true,
    });
    lib_mod.addImport("mer", mer_mod);

    const main_mod = b.createModule(.{
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
        .strip = if (optimize != .Debug) true else null,
    });
    main_mod.addImport("mer", mer_mod);
    main_mod.addImport("runtime", runtime_mod);
    main_mod.addImport("lib", lib_mod);
    addDirModules(b, main_mod, mer_mod, lib_mod, "app");
    addDirModules(b, main_mod, mer_mod, lib_mod, "api");
    addRoutesModule(b, main_mod, mer_mod, lib_mod);

    const exe = b.addExecutable(.{ .name = "app", .root_module = main_mod });
    b.installArtifact(exe);

    // zig build codegen
    const codegen_mod = b.createModule(.{
        .root_source_file = b.path("tools/codegen.zig"),
        .target = b.graph.host,
        .optimize = .Debug,
    });
    codegen_mod.addImport("runtime", runtime_mod);
    const codegen_exe = b.addExecutable(.{
        .name = "codegen",
        .root_module = codegen_mod,
    });
    const run_codegen = b.addRunArtifact(codegen_exe);
    run_codegen.setCwd(b.path("."));
    b.step("codegen", "Regenerate src/generated/routes.zig").dependOn(&run_codegen.step);

    // Auto-run codegen before compiling (fresh clones just work).
    exe.step.dependOn(&run_codegen.step);

    // zig build serve
    const run_exe = b.addRunArtifact(exe);
    run_exe.step.dependOn(b.getInstallStep());
    if (b.args) |args| run_exe.addArgs(args);
    b.step("serve", "Start the dev server").dependOn(&run_exe.step);

    // zig build test
    const test_mod = b.createModule(.{
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });
    test_mod.addImport("mer", mer_mod);
    test_mod.addImport("runtime", runtime_mod);
    test_mod.addImport("lib", lib_mod);
    addDirModules(b, test_mod, mer_mod, lib_mod, "app");
    addDirModules(b, test_mod, mer_mod, lib_mod, "api");
    addRoutesModule(b, test_mod, mer_mod, lib_mod);
    const run_tests = b.addRunArtifact(b.addTest(.{ .root_module = test_mod }));
    run_tests.step.dependOn(&run_codegen.step);

    // Tests in imported build modules are not collected by the main test root.
    const module_test_mod = b.createModule(.{
        .root_source_file = b.path("src/tests.zig"),
        .target = target,
        .optimize = optimize,
    });
    const run_module_tests = b.addRunArtifact(b.addTest(.{ .root_module = module_test_mod }));

    const test_step = b.step("test", "Run the WikiBase frontend tests");
    test_step.dependOn(&run_tests.step);
    test_step.dependOn(&run_module_tests.step);
}

fn addRoutesModule(
    b: *std.Build,
    mod: *std.Build.Module,
    mer_mod: *std.Build.Module,
    lib_mod: *std.Build.Module,
) void {
    const routes_mod = b.createModule(.{
        .root_source_file = b.path("src/generated/routes.zig"),
    });
    routes_mod.addImport("mer", mer_mod);
    addDirModules(b, routes_mod, mer_mod, lib_mod, "app");
    addDirModules(b, routes_mod, mer_mod, lib_mod, "api");
    mod.addImport("routes", routes_mod);
}

fn addDirModules(
    b: *std.Build,
    mod: *std.Build.Module,
    mer_mod: *std.Build.Module,
    lib_mod: *std.Build.Module,
    dir: []const u8,
) void {
    const layout_path = b.fmt("{s}/layout.zig", .{dir});
    const layout_mod: ?*std.Build.Module = blk: {
        std.Io.Dir.cwd().access(b.graph.io, layout_path, .{}) catch break :blk null;
        const m = b.createModule(.{ .root_source_file = b.path(layout_path) });
        m.addImport("mer", mer_mod);
        m.addImport("lib", lib_mod);
        mod.addImport(b.fmt("{s}/layout", .{dir}), m);
        break :blk m;
    };
    var d = std.Io.Dir.cwd().openDir(b.graph.io, dir, .{ .iterate = true }) catch return;
    defer d.close(b.graph.io);
    var walker = d.walk(b.allocator) catch return;
    defer walker.deinit();
    while (walker.next(b.graph.io) catch null) |entry| {
        if (entry.kind != .file) continue;
        if (!std.mem.endsWith(u8, entry.path, ".zig")) continue;
        if (std.mem.eql(u8, entry.path, "layout.zig")) continue;
        const file_path = b.fmt("{s}/{s}", .{ dir, entry.path });
        const import_name = b.fmt("{s}/{s}", .{ dir, entry.path[0 .. entry.path.len - 4] });
        const route_mod = b.createModule(.{ .root_source_file = b.path(file_path) });
        route_mod.addImport("mer", mer_mod);
        route_mod.addImport("lib", lib_mod);
        if (layout_mod) |lm| route_mod.addImport(b.fmt("{s}/layout", .{dir}), lm);
        mod.addImport(import_name, route_mod);
    }
}
